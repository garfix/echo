[
	// John drives / She died

	rule:
		S => NP VP,
	semantics: {
		S.sem = NP.sem and VP.sem and subject(S.event, S.subject) and object(S.event, S.object) and mood(S.event, Declarative);
    	S.event = VP.event;
    	S.subject = NP.object;
    	S.object = VP.object
	}
]

[
	// Drive! / Book that flight / She died

	rule:
		S => VP,
	semantics: {
		S.sem = VP.sem and object(S.event, S.object) and mood(S.event, Imperative);
		S.event = VP.event;
		S.object = VP.object
	}
]

[
	// Who Is John? / How many children had Lord Byron? / Who were Lord Byron's parents?

	rule:
		S => WhADVP VP NP,
	semantics: {
		S.sem = WhADVP.sem and VP.sem and NP.sem and subject(S.event, S.subject) and object(S.event, S.object) and request(S.request) and mood(S.event, Interrogative);
        S.event = VP.event;
        S.subject = NP.object;
        S.subject = WhADVP.subject;
        S.object = WhADVP.object;
        S.request = WhADVP.request;
        S.event = WhADVP.event
	}
]

[
	// Where was John born?

	rule:
        S => WhADVP auxBe NP VP,
    semantics: {
		S.sem = WhADVP.sem and auxBe.sem and NP.sem and VP.sem and subject(S.event, S.subject) and request(S.request) and mood(S.event, Interrogative);
		S.event = WhADVP.event;
		S.event = VP.event;
		S.subject = NP.object;
		S.request = WhADVP.request;
		S.event = auxBe.event
    }
]

[
	// Was John driving?

	rule:
        S => aux NP VP,
    semantics: {
		S.sem = NP.sem and VP.sem and subject(S.event, S.subject) and mood(S.event, Interrogative);
		S.event = VP.event;
		S.subject = NP.object
    }
]

[
	// Was the car driven by John?

	rule:
        S => aux NP1 VP passivisationPreposition NP2,
    semantics: {
        S.sem = NP1.sem and NP2.sem and VP.sem and subject(S.event, S.subject) and object(S.event, S.object) and mood(S.event, Interrogative) and voice(S.event, Passive);
        S.event = VP.event;
        S.subject = NP2.object;
        S.object = NP1.object
    }
]

[
	// Was John a fool?
	// Was Ada Lovelace the daughter of Lord Byron?

	rule:
        S => auxBe NP1 NP2,
    semantics: {
       S.sem = auxBe.sem and NP1.sem and NP2.sem and subject(S.event, S.subject) and complement(S.event, S.complement) and mood(S.event, Interrogative);
       S.complement = NP2.object;
       S.subject = NP1.object
    }

]

[
	// How old was Mary Shelley?

	rule:
        S => WhADVP auxBe NP,
    semantics: {
        S.sem = WhADVP.sem and auxBe.sem and NP.sem and subject(S.event, S.subject) and modifier(S.event, S.complement) and request(S.request) and mood(S.event, Interrogative);
        S.complement = WhADVP.object;
        S.event = auxBe.event;
        S.subject = NP.object;
        S.request = WhADVP.request
    }

]

[
	// How old was Mary Shelley when she died?

	rule:
		S1 => S2 SBar,
	semantics: {
		S1.sem = S2.sem and SBar.sem;
		S1.event = S2.event;
		S1.subject = S2.subject;
		S1.object = S2.object;
		S1.event = SBar.superEvent;
		S1.request = S2.request
	}
]

[
	// S-Bar, see 'The structure of modern english' - Brinton (2000) - p. 230

	rule:
		SBar => whAdverb S,
	semantics: {
		SBar.sem = whAdverb.sem and S.sem;
		SBar.superEvent = whAdverb.event;
		SBar.subEvent = whAdverb.adjunct;
		SBar.subEvent = S.event
	}
]

// Verb Phrase

[
	// drives

	rule:
		VP => verb,
	semantics: {
		VP.sem = verb.sem;
		VP.event = verb.event
	}
]

[
	// book that flight! / sees the book
	// Name Lord Byron's children.

	rule:
		VP => verb NP,
	semantics: {
		VP.sem = verb.sem and NP.sem and object(VP.event, VP.object);
		VP.event = verb.event;
		VP.object = NP.object
	}
]

[
	// gives Mary flowers

	rule:
		VP => verb NP1 NP2,
	semantics: {
		S.sem = verb.sem and NP1.sem and NP2.sem and subject(S.event, S.subject) and object(S.event, S.object);
		S.event = verb.event;
		S.subject = NP1.object;
		S.object = NP2.object
	}
]

[
	// ben 43 jaar oud

	rule:
		VP => copula AP,
	semantics: {
		VP.sem = AP.sem;
		VP.event = AP.event;
		VP.object = AP.object
	}
]

[
	// driven by John

	rule:
		VP => verb PP,
	semantics: {
		VP.sem = verb.sem and PP.sem;
		VP.event = verb.event;
		VP.event = PP.superObject
	}
]

// Wh-adverb phrase


[
	// who

	rule:
        WhADVP => whAdverb,
    semantics: {
        WhADVP.sem = whAdverb.sem;
        WhADVP.request = whAdverb.adjunct;
        WhADVP.subject = whAdverb.subject
    }
]

[
	// where, when

	rule:
        WhADVP => whAdverb,
    semantics: {
        WhADVP.sem = whAdverb.sem;
        WhADVP.request = whAdverb.adjunct;
        WhADVP.event = whAdverb.event
    }
]

[
	// which car, how many children

	rule:
        WhADVP => whwordNP NP,
    semantics: {
		WhADVP.sem = whwordNP.sem and NP.sem;
		WhADVP.object = whwordNP.object;
		WhADVP.object = NP.object;
		WhADVP.request = whwordNP.adjunct
    }
]

[
	// how old

	rule:
        WhADVP => whwordNP AP,
    semantics: {
		WhADVP.sem = whwordNP.sem and AP.sem;
		WhADVP.object = whwordNP.object;
		WhADVP.object = AP.object;
		WhADVP.request = whwordNP.adjunct
    }
]

// Noun Phrase

[
	// John

	rule:
        NP => PN,
    semantics: {
		NP.sem = PN.sem;
        NP.object = PN.object
    }
]

[
	// he

	rule:
        NP => pronoun,
    semantics: {
		NP.sem = pronoun.sem;
        NP.object = pronoun.object
    }
]

[
	// the car

	rule:
        NP => DP NBar,
    semantics: {
		NP.sem = DP.sem and NBar.sem;
        NP.object = NBar.object;
        NP.object = DP.entity
    }
]

[
	// (large) car (in the lot)

	rule:
        NP => NBar,
    semantics: {
		NP.sem = NBar.sem;
        NP.object = NBar.object
    }
]

// N-Bar

[
	// car

	rule:
        NBar => noun,
    semantics: {
		NBar.sem = noun.sem;
        NBar.object = noun.object
    }
]

[
	// blue car

	rule:
        NBar1 => AP NBar2,
    semantics: {
		NBar1.sem = AP.sem and NBar2.sem;
        NBar1.object = NBar2.object
    }
]

[
	// car in the lot
	// the author of Paradise Lost

	// TODO: change NBar into NBar1 (strange DBPedia fail)

	rule:
        NBar => NBar2 PP,
    semantics: {
		NBar.sem = NBar2.sem and PP.sem;
        NBar.object = NBar2.object;
        NBar.object = PP.superObject
    }
]

// Proper Name

[
	// John, "Lord Byron (the first)"

	rule:
        PN => propernoun,
    semantics: {
		PN.sem = name(PN.object, propernoun.text);
        PN.object = propernoun.object
    }
]

[
	// Lord Byron, Ada Lovelace

	rule:
        PN => propernoun1 propernoun2,
    semantics: {
		PN.sem = name(PN.object, propernoun1.text + " " + propernoun2.text);
        PN.object = propernoun1.object;
        PN.object = propernoun2.object
    }
]

[
	// Jan de Wit

	rule:
        PN => propernoun1 propernoun2 propernoun3,
    semantics: {
		PN.sem = name(PN.object, propernoun1.text + " " + propernoun2.text + " " + propernoun3.text);
        PN.object = propernoun1.object;
        PN.object = propernoun2.object;
        PN.object = propernoun3.object
    }
]

[
	// Jan de Wit

	rule:
        PN => propernoun1 insertion propernoun2,
    semantics: {
        PN.sem = name(PN.object, propernoun1.text + " " + insertion.text + " " + propernoun2.text);
        PN.object = propernoun1.object;
        PN.object = propernoun2.object
    }
]

// Prepositional Phrase

[
	rule:
		PP => preposition NP,
	semantics: {
		PP.sem = preposition.sem and NP.sem;
		PP.superObject = preposition.superObject;
		PP.subObject = preposition.subObject;
		PP.subObject = NP.object
	}
]

// Adjectival Phrase

[
	// red

	rule:
		AP => adjective,
	semantics: {
		AP.sem = adjective.sem
	}
]

[
	// fiercely barking

	rule:
		AP => AdvP adjective,
	semantics: {
		AP.sem = AdvP.sem and adjective.sem
	}
]

[
	// bright red

	rule:
		AP => degreeAdverb adjective,
	semantics: {
		AP.sem = degreeAdverb.sem and adjective.sem
	}
]

[
	// (i am) 43 years old

	rule:
		AP => NP adjective,
	semantics: {
		AP.sem = NP.sem and adjective.sem
	}
]

// Adverbial Phrase

[
	// quickly

	rule:
        AdvP => adverb,
    semantics: {
        AP.sem = AdvP.sem and adverb.sem
    }
]

[
	// very quickly

	rule:
        AdvP => degreeAdverb adverb,
    semantics: {
        AP.sem = AdvP.sem and degreeAdverb.sem and adverb.sem
    }
]

// Determiner Phrase

[
	// the
	rule:
        DP => determiner,
    semantics: {
        DP.sem = determiner.sem
    }
]

[
	// Byron's

	rule:
        DP => NP possessiveMarker,
    semantics: {
        DP.sem = NP.sem and possessiveMarker.sem;
        DP.subObject = possessiveMarker.subObject;
        DP.entity = possessiveMarker.superObject;
        DP.subObject = NP.object
    }
]

[
	// 43 (years old)
	// 2 (blue cars)

	rule:
		DP => numeral,
	semantics: {
		DP.sem = numeral.sem
	}
]

