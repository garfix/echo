# Clause

// Yes, Harry met Sally
[
	rule:
		S => adverb punctuationMark SDecl,
	condition1:
		qualification(S.event, S.qualification),
	bind: {
		SDecl.event = S.event;
		adverb.event = S.qualification
	}
]

// Declarative sentences
[
	rule:
		S => SDecl,
	condition1:
		mood(S.event, Declarative),
	bind: {
		SDecl.event = S.event
	}
]

# Declarative sentence

// Copular sentence with complement
// The ball is a toy.
[
	rule:
		SDecl => NP1 auxBe NP2,
	condition1:
		subject(S.event, S.subject) and complement(S.event, S.complement),
	bind: {
		NP1.entity = S.subject;
		NP2.entity = S.complement
	}
]

// Copular sentence with modifier
// The ball is round.
[
	rule:
		SDecl => NP auxBe AP,
	condition1:
		subject(S.event, S.subject) and modifier(S.event, S.complement),
	bind: {
		NP.entity = S.subject;
		AP.entity = S.complement
	}
]

// Passive declarative
// Lord Byron was influenced by John Milton
// See: The structure of modern english (2000), p 202
[
	rule:
		SDecl => NP auxPsv VP,
	condition1:
		subject(S.event, S.subject) and object(S.event, S.object) and voice(S.event, Passive),
	bind: {
		NP.entity = S.object;
		auxPsv.event = S.event;
		VP.event = S.event;
		VP.subject = S.subject;
		VP.object = S.object
	}
]

[
	rule:
		SDecl => NP auxBe VP,
	condition1:
		subject(S.event, S.subject) and aspect(S.event, Perfect),
	bind: {
		NP.entity = S.subject;
		auxBe.event = S.event;
		VP.event = S.event
	}
]

// John Walks
[
	rule:
		SDecl => NP VP,
	condition1:
		subject(S.event, S.subject),
	bind: {
		NP.entity = S.subject;
		VP.event = S.event
	}
]

# Noun Phrase


// Lord Byron
[
	rule:
		NP => propernoun,
	condition1:
		name(NP.entity, ?name),
	bind: {
		propernoun.entity = NP.entity
	}
]

// the author of Paradise Lost
[
	rule:
		NP => DP NBar,
	bind: {
		DP.entity = NP.entity;
		NBar.entity = NP.entity
	}
]

// Lord Byron
[
	rule:
		NP => noun,
	condition1:
		name(NP.entity, ?name),
	bind: {
		noun.entity = NP.entity
	}
]

# Determiner Phrase

// the
[
	rule:
		DP => determiner,
	bind: {
		determiner.entity = DP.entity
	}
]

# determiner

// the
[
	rule:
		determiner => word,
	condition1:
		determiner(determiner.entity, ?det),
	word:
		determiner(determiner.entity, ?det)
]

# NBar

// author of Paradise Lost
[
	rule:
		NBar => NBar PP,
	condition1:
		link(?link, NBar.entity, ?entity),
	bind: {
		NBar.entity = NBar.entity;
		PP.superObject = NBar.entity
	}
]

// author
[
	rule:
		NBar => noun,
	bind: {
		noun.object = NBar.entity
	}
]

# Propositional Phrase

// of Paradise Lost
[
	rule:
		PP => preposition NP,
	condition1:
    	link(?link, PP.superObject, PP.subObject),
    bind: {
        preposition.superObject = PP.superObject;
        preposition.subObject = PP.subObject;
        NP.entity = PP.subObject
    }
]

# Adjective Phrase

// 53 years old
[
	rule:
		AP => NP adjective,
	condition1:
		modifier(AP.entity, AP.modifier),
	bind: {
		NP.entity = AP.modifier;
		adjective.object = AP.entity
	}
]

// old
[
	rule:
		AP => adjective,
	bind: {
		adjective.object = AP.entity
	}
]

# adjective

// old
[
	rule:
		adjective => word,
	condition1:
		isa(adjective.object, ?type),
	word:
		isa(adjective.object, ?type)
]

# noun

// author
[
	rule:
		noun => word,
	condition1:
		isa(noun.object, ?type),
	word:
		isa(noun.object, ?type)
]

# propernoun

// Lord Byron
[
	rule:
		propernoun => word,
	condition1:
		name(propernoun.entity, ?name),
	word:
		name(propernoun.entity, ?name)
]

# Verb Phrase

// influenced by John Milton
[
	rule:
		VP => pastParticipleVerb passivisationPreposition NP,
	condition1:
		isa(VP.event, ?type) and subject(VP.event, VP.subject) and voice(VP.event, Passive),
	bind: {
		pastParticipleVerb.event = VP.event;
		passivisationPreposition.event = VP.event;
		passivisationPreposition.subject = VP.subject;
		NP.entity = VP.subject
	}
]

// married to Anne Isabella Milbanke
[
	rule:
		VP => pastParticipleVerb PP,
	condition1:
		isa(VP.event, ?type) and link(?link, VP.event, VP.object) and aspect(VP.event, Perfect),
	bind: {
		pastParticipleVerb.event = VP.event;
		PP.entity = VP.object
	}
]

// died in Missolonghi
[
	rule:
		VP => simpleVerb PP,
	condition1:
		isa(VP.event, ?type) and link(?link, VP.event, VP.object),
	bind: {
		simpleVerb.event = VP.event;
		PP.entity = VP.object
	}
]

// influenced Lord Byron
[
	rule:
		VP => simpleVerb NP,
	condition1:
		isa(VP.event, ?type) and object(VP.event, VP.object),
	bind: {
		simpleVerb.event = VP.event;
		NP.entity = VP.object
	}
]

// walks
[
	rule:
		VP => simpleVerb,
	condition1:
		isa(VP.event, ?type),
	bind: {
		simpleVerb.event = VP.event
	}
]

# passivisationPreposition

// by (passivisation)
[
	rule:
		passivisationPreposition => word
]

# punctuationMark

// ,
[
	rule:
		punctuationMark => word
]

# conjunction

// and
[
	rule:
		conjunction => word,
	condition1:
		link(?cp, conjunction.node, conjunction.firstEntity, conjunction.secondEntity),
	word:
		link(?cp, conjunction.node, conjunction.firstEntity, conjunction.secondEntity)
]

# auxPsv

// was (passive)
[
	rule:
		auxPsv => word,
	condition1:
		tense(auxPsv.event, ?tense),
	word:
		tense(auxPsv.event, ?tense)
]

# auxBe

// was (auxilliary)
[
	rule:
		auxBe => word,
	condition1:
		tense(auxBe.event, ?tense),
	word:
		tense(auxBe.event, ?tense)
]

# adverb

// yes
[
	rule:
		adverb => word,
	condition1:
		isa(adverb.event, ?type),
	word:
		isa(adverb.event, ?type)
]

# preposition

// of
[
	rule:
		preposition => word,
	condition1:
		link(?prep, ?super, ?sub),
	word:
		link(?prep, ?super, ?sub)
]

# pastParticipleVerb

[
	rule:
		pastParticipleVerb => word,
	condition1:
		isa(pastParticipleVerb.event, ?type) and isa(pastParticipleVerb.event, ?type),
	word:
		isa(pastParticipleVerb.event, ?type) and tense(pastParticipleVerb.event, ?tense)
]

# simpleVerb

// verb, explicit tense
[
	rule:
		simpleVerb => word,
	condition1:
		isa(simpleVerb.event, ?type) and tense(simpleVerb.event, ?tense),
	word:
		isa(simpleVerb.event, ?type) and tense(simpleVerb.event, ?tense)
]

// verb, implicit
[
	rule:
		simpleVerb => word,
	condition1:
		isa(simpleVerb.event, ?type),
	word:
		isa(simpleVerb.event, ?type) and tense(simpleVerb.event, Present)
]